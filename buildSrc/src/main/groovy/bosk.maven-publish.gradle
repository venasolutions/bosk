
/*
 * Settings related to publishing Maven artifacts.
 * Actual technological dependencies of Bosk should go into the project's Gradle files.
 */

plugins {
	id 'java-library'
	id 'maven-publish'
	id 'signing'
}

var publicationType = version.endsWith('-SNAPSHOT') ? 'snapshot' : 'release'

java {
	withSourcesJar()
	withJavadocJar()
}

jar {
	manifest {
		// https://docs.gradle.org/current/userguide/building_java_projects.html#sec:jar_manifest
		attributes("Implementation-Version": archiveVersion)
	}
}

// https://central.sonatype.org/publish/publish-gradle/#jar-files

javadoc {
	failOnError = false
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			versionMapping {
				usage('java-api') {
					fromResolutionOf('runtimeClasspath')
				}
				usage('java-runtime') {
					fromResolutionResult()
				}
			}
			pom {
				name = 'Bosk'
				packaging = 'jar'
				description = 'Control plane state management library'
				url = 'http://bosk.works'
				licenses {
					license {
						name = 'The Apache License, Version 2.0'
						url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}
				developers {
					developer {
						id = 'pdoyle'
						name = 'Patrick Doyle'
						email = 'pdoyle@venacorp.com'
					}
					developer {
						id = 'gjohnson'
						name = 'Grady Johnson'
						email = 'gjohnson@venacorp.com'
					}
				}
//				scm {
//					connection = 'scm:git:git://example.com/my-library.git'
//					developerConnection = 'scm:git:ssh://example.com/my-library.git'
//					url = 'http://example.com/my-library/'
//				}
			}
		}
	}
	repositories {
		maven {
			def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
			def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
			url version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

			credentials {
				username System.getEnv("OSSRH_USERNAME")
				password System.getEnv("OSSRH_TOKEN")
			}
		}

		/*
		maven {
			name = "VenaArtifactoryDeprecated"
			credentials( PasswordCredentials )
			allowInsecureProtocol = true
			url = "http://artifactory.devops.vena.vpn:8081/artifactory/libs-${publicationType}-local/"
		}
		maven {
			name = "VenaArtifactory"
			credentials( PasswordCredentials )
			url = "https://venasolutions.jfrog.io/artifactory/vena-libs-${publicationType}-local/"
		}
		*/
	}
}

signing {
	sign publishing.publications.mavenJava
}

javadoc {
	if(JavaVersion.current().isJava9Compatible()) {
		options.addBooleanOption('html5', true)
	}
}
